/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'lbruenings' at '27.11.15 15:08' with Gradle 2.8
 *
 * This generated file contains a sample Groovy project to get you started.
 * For more details take a look at the Groovy Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.8/userguide/tutorial_groovy_projects.html
 */

apply plugin: 'idea'
apply plugin: 'groovy'

// In this section you declare where to find the dependencies of your project
allprojects {
    repositories {
        // Use 'jcenter' for resolving your dependencies.
        // You can declare any Maven/Ivy/file repository here.
        jcenter()
    }
}

dependencies {
    // We use the latest groovy 2.x version for building this library
    compile "org.codehaus.groovy:groovy-all:2.3.11"

    // We use the awesome Spock testing and specification framework
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3'
    testCompile 'junit:junit:4.12'
}


['2.4.5', '2.4.4', '2.4.3','2.4.2','2.4.1','2.4.0', '2.3.11' , '2.3.10', '2.3.5', '2.3.1'].each { groovyVersion ->
    project(":groovy-$groovyVersion") {
        apply plugin: 'groovy'
        dependencies {
            // We use the latest groovy 2.x version for building this library
            compile "org.codehaus.groovy:groovy-all:$groovyVersion"

            // We use the awesome Spock testing and specification framework
            if (groovyVersion.startsWith('2.4')) {
                testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
            } else {
                testCompile 'org.spockframework:spock-core:1.0-groovy-2.3'
            }
            testCompile 'junit:junit:4.12'
        }

        sourceSets.main.java.srcDir file('../src/main/java')
        sourceSets.main.groovy.srcDir file('../src/main/groovy')
        sourceSets.test.groovy.srcDir file('../src/test/groovy')

        test {
            outputs.upToDateWhen { false }  // Always run tests
        }
    }
}
